;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Section: Imports
;;

(ns riemann.config
  (:import  [java.util HashMap])
  (:require riemann.core
            riemann.transport
            [clojure.java.io   :as io]
            [cheshire.core     :as cheshire]
            [clj-http.client   :as http]
            [langohr.core      :as l-core]
            [langohr.channel   :as l-channel]
            [langohr.queue     :as l-queue]
            [langohr.consumers :as l-consumers])
  (:use     [riemann.service   :only [Service ServiceEquiv]]))

;;
;; End Section: Imports
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Section: RabbitMQ services
;;

; reset below
(def amqp-connection-service)

(defn queue-message-handler [core]
  (fn [ch {:keys [content-type delivery-tag type] :as meta} ^bytes payload]
    (let [event (cheshire/parse-string (String. payload) true)]
      (riemann.core/stream! core event))))

(defrecord AMQPConnection [connection]
  ServiceEquiv
  (equiv? [this other]
    (instance? AMQPConnection other))

  Service
  (conflict? [this other]
    false)

  (reload! [this new-core]
    true)

  (start! [this]
    (locking this
      (when (not @connection)
        (let [new-connection (l-core/connect)]
          (info "AMQP connection opened")
          (reset! connection new-connection)))))

  (stop! [this]
    (locking this
      (when @connection
        (info "AMQP connection closing")
        (l-core/close @connection)
        (reset! connection nil)))))

(defn amqp-connection []
  ; connection is opened here because we need to make sure
  ; it is started before the manager queue service is started
  (let [new-connection (l-core/connect)]
    (info "AMQP connection opened")
    (service! (AMQPConnection. (atom new-connection)))))

(defrecord AMQPQueueConsumer [queue-name
                              core
                              channel]
  ServiceEquiv
  (equiv? [this other]
    (and
      (instance? AMQPQueueConsumer other)
      (= queue-name (:queue-name other))))

  Service
  (conflict? [this other]
    (and
      (instance? AMQPQueueConsumer other)
      (= queue-name (:queue-name other))))

  (reload! [this new-core]
    (reset! core new-core))

  (start! [this]
    (locking this
      (when (not @channel)
        (let [connection  (:connection amqp-connection-service)
              new-channel (l-channel/open @connection)]
          (l-queue/declare new-channel
                           queue-name
                           :exclusive false
                           :durable false
                           :auto-delete true)
          (l-consumers/subscribe new-channel
                                 queue-name
                                 (queue-message-handler @core)
                                 :auto-ack true)
          (info "AMQP" queue-name "consumer started")
          (reset! channel new-channel)))))

  (stop! [this]
    (locking this
      (when @channel
        (info "AMQP" queue-name "consumer stopping")
        (l-core/close @channel)
        (reset! channel nil)))))

(defn amqp-queue-consumer [queue-name]
  (service! (AMQPQueueConsumer. queue-name
                                (atom nil)
                                (atom nil))))

;;
;; End Section: RabbitMQ services
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Section: Global riemann configuration
;;

(logging/init {:file "/tmp/riemann.log"})

;;
;; End Section: Global riemann configuration
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Section: User definitions
;; Mostly helper predicates, streams, etc... used
;; when writing policies
;;

; example: (where* (is-node-name "some_name") ...)
(defn is-node-name [node-name]
  (fn [event]
    (= node-name (:node_name event))))

(defn execute-workflow
  ([workflow deployment-id]
    (execute-workflow workflow deployment-id {}))
  ([workflow deployemnt-id opts]
    (let [manager-ip        "127.0.0.1"
          manager-rest-port 8000
          base-uri          (str "http://" manager-ip ":" manager-rest-port)
          endpoint          (str "/deployments/" deployment-id "/executions")
          resource-uri      (str base-uri endpoint)
          socket-timeout    (get opts :socket-timeout 1000)
          conn-timeout      (get opts :conn-timeout 1000)
          parameters        (get opts :parameters {})]
      (http/post resource-uri
        {:content-type   :json
         :accept         :json
         :socket-timeout socket-timeout
         :conn-timeout   conn-timeout
         :body           (cheshire/generate-string {:workflow_id workflow
                                                    :parameters parameters})}))))

;;
;; End Section: User definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Section: Deployments core administration
;;

(def config-lock             (Object.))
(def cores-map               (HashMap.))
(def amqp-connection-service (amqp-connection))

(defn start-config! [& children]
  (fn [event]
    (locking config-lock
      (let [config-path   (:config_path event)
            deployment-id (:deployment_id event)
            ok-path       (str config-path "/ok")]

        ; prepare config cores
        (reset! core      (riemann.core/core))
        (reset! next-core (riemann.core/core))

        ; load configuration files and apply to create new core
        (include config-path)
        (apply!)

        ; external indication new node is up
        (spit ok-path "ok")

        (.put cores-map deployment-id @core)))))

(defn stop-config! [& children]
  (fn [event]
    (locking config-lock
      (let [ok-path       (str (:config_path event) "/ok")
            deployment-id (:deployment_id event)
            ; get core instance to stop and remove it from the map
            stopped-core  (.remove cores-map deployment-id)]

        (when stopped-core
          (io/delete-file ok-path)
          (riemann.core/stop! stopped-core))))))

;;
;; End Section: Deployments core administration
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Section: Management core configuration
;;

(amqp-queue-consumer "manager-riemann")

(periodically-expire 5)

(let [index (index)]
  (streams
    (default :ttl 60 index)
    (where (and (service "cloudify.configuration")
                (state "start"))
      (start-config!))
    (where (and (service "cloudify.configuration")
                (state "stop"))
      (stop-config!))))

;;
;; End Section: Management core configuration
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
