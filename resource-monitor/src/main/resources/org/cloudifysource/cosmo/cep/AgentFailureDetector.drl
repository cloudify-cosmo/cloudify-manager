/*******************************************************************************
 * Copyright (c) 2013 GigaSpaces Technologies Ltd. All rights reserved
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *******************************************************************************/

import org.cloudifysource.cosmo.agent.messages.ProbeAgentMessage
import org.cloudifysource.cosmo.cep.messages.AgentStatusMessage
import org.cloudifysource.cosmo.statecache.messages.StateChangedMessage
import org.cloudifysource.cosmo.cep.Agent

declare StateChangedMessage
@role(event)
end

declare Agent
@role(event)
end

declare ProbeAgentMessage
@role(event)
end

declare AgentStatusMessage
@role(event)
end

rule "Fire agent probe every 20 seconds"
    dialect "mvel"
when
    Agent( $agentId : agentId)
    not ProbeAgentMessage( agentId == $agentId) over window:time(20s)
then
    message = new ProbeAgentMessage()
    message.agentId = $agentId
    insert(message)
    channels["output"].send( message );
end

rule "Change state to not_reachable if no response within 1 minute"
    dialect "mvel"
when
    $request: ProbeAgentMessage( $agentId : agentId)
    not AgentStatusMessage( agentId == $agentId, this after[0, 1m ] $request )
then
    message = new StateChangedMessage()
    message.resourceId = $agentId
    message.reachable = false
    channels["output"].send( message );
end

rule "Change state to reachable if response within 1 minute"
    dialect "mvel"
when
    $request: ProbeAgentMessage( $agentId : agentId)
    AgentStatusMessage( agentId == $agentId, this after[0, 1m ] $request )
then
    message = new StateChangedMessage()
    message.resourceId = $agentId
    message.reachable = true
    channels["output"].send( message );
end