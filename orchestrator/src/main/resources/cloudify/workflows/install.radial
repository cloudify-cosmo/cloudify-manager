define initialize_nodes

    log message: 'Reloading policies'
    event event: { "stage" => "Initializing monitoring policies" }
    reload_riemann_config policies: '$plan.policies',
                          rules: '$plan.rules',
                          policies_events: '$plan.policies_events'

    log message: 'processing nodes: ${plan.nodes}'
    concurrent_iterator on: '$plan.nodes', to_f: 'node', merge_type: 'ignore'
        log message: 'processing node: ${f:node}'
        iterator on: '$f:node.relationships', to_v: 'relationship'
            log message: 'waiting for node: ${node.id} relationship: ${v:relationship}'
            state node_id: '${v:relationship.target_id}', reachable: 'true'
            sequence if: '${v:relationship.bind_at} == pre_started'
                log message: 'performing relationship bindings of node:${node.id} / ${v:relationship}'
                set 'v:relationship_workflow': '$v:relationship.workflow'
                relationship_workflow relationship: '$v:relationship'
        unset 'v:relationship'

        event event: { "stage" => "Initializing node" }
        log message: 'executing init workflow for node: ${node.id}'
        set 'v:init': "$f:node.workflows.init"
        init

        state node_id: '${node.id}', reachable: 'true'
        iterator on: '$f:node.relationships', to_v: 'relationship'
            sequence if: '${v:relationship.bind_at} == post_started'
                log message: 'performing relationship late bindings of node:${node.id} / ${v:relationship}'
                set 'v:relationship_workflow': '$v:relationship.workflow'
                relationship_workflow relationship: '$v:relationship'
        unset 'v:relationship'
